apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

sourceSets.main.java.srcDirs = [ "src/" ]
sourceCompatibility = 1.6

configurations {
    deployerJars
}

repositories {
    mavenCentral()
    jcenter()
}

jar {
    baseName = 'gdx-lml'
    version =  '0.1.155'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.konfigurats.lml"
archivesBaseName = "gdx-lml"
version = "0.1.155"

ext {
    gdxVersion = '1.5.5'
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
    deployerJars "org.apache.maven.wagon:wagon-http:2.2"
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Gdx Lml'
        packaging 'jar'
        description 'HTML-like templates for LibGDX Scene2D UI.'
        url 'http://www.example.com/example-application'

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'mj'
            name 'MJ'
            email 'john.hervicc@gmail.com'
          }
        }
      }
    }
  }
}

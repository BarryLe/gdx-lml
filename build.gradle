apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

sourceSets.main.java.srcDirs = [ "src/" ]
sourceCompatibility = 1.6

ext {
	libVersion = '0.7'
    gdxVersion = '1.6.0'
    gwtVersion = '2.6.0'
    isSnapshot = '-SNAPSHOT'
}

group = "com.github.czyzby"
archivesBaseName = "gdx-autumn-gwt"
version = "$libVersion.$gdxVersion$isSnapshot"

configurations {
    deployerJars
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

jar {
    from project.sourceSets.main.allSource
    from project.sourceSets.main.output
    baseName = 'gdx-autumn-gwt'
}

javadoc {
    options.addStringOption("sourcepath", "")
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
    compile "com.google.gwt:gwt-user:$gwtVersion"
    compile "com.google.gwt:gwt-user:$gwtVersion:sources"
    compile "com.google.gwt:gwt-dev:$gwtVersion"
    compile "com.google.gwt:gwt-dev:$gwtVersion:sources"
    compile "com.github.czyzby:gdx-autumn:$libVersion.$gdxVersion$isSnapshot"
    compile "com.github.czyzby:gdx-autumn:$libVersion.$gdxVersion$isSnapshot:sources"
    compile "com.github.czyzby:gdx-kiwi:$libVersion.$gdxVersion$isSnapshot:sources"
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
    deployerJars "org.apache.maven.wagon:wagon-http:2.2"
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Gdx Autumn GWT'
        packaging 'jar'
        description 'GWT natives for LibGDX Autumn.'
        url 'http://github.com/czyzby/gdx-autumn-gwt'

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        
        scm {
          connection 'scm:git:git@github.com:czyzby/gdx-autumn-gwt.git'
          developerConnection 'scm:git:git@github.com:czyzby/gdx-autumn-gwt.git'
          url 'http://github.com/czyzby/gdx-autumn-gwt/'
        }

        developers {
          developer {
            id 'mj'
            name 'MJ'
            email 'john.hervicc@gmail.com'
          }
        }
      }
    }
  }
}